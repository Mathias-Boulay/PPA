// 2019-04-21 20:30:58
#event create|Récupération du texte + placement:
Can_navigate=false;
Choice=1;
alarm_set(0,30);
texts=script_word_light(63,65);
draw_set_font(global.font_normal);
draw_set_halign(fa_center);
draw_set_valign(fa_top);
image_xscale=string_width(texts[0]);
image_yscale=string_height(texts[0])+28;
//Placement:
x=round(view_xview[0]+(view_wview[0]/2)-(image_xscale/2));
y=round(view_yview[0]+(view_hview[0]/2)-(image_yscale/2));

#event alarm0|Réactivation de la navigation.
Can_navigate=true;

#event step|Gestion du bouton:
if abs(global.axislh[0]) > 0.2 && Can_navigate{
    Can_navigate=false;
    alarm_set(0,10);
    if Choice = 1{Choice=2;}else{Choice=1;}
    }
if global.face2[0]{
    instance_destroy();
    }
if global.face1[0]{
    if Choice = 1{
        //On quitte le niveau: on déclenche l'animation de soulignement.
        with(object_right_hand_pause){
            path_start(temp_path,2,path_action_stop,false);
            STATUS = "UNDERLINE";
            }
        }
    else{
        //On revient dans le menu de pause.
        instance_destroy();
        }
    }


#event draw|Dessiner la prompt,incluant les multiples morceaux.
draw_self();
draw_set_font(global.font_normal);
draw_set_halign(fa_center);
draw_set_valign(fa_top);
//Texte
draw_text(x+(image_xscale/2),y,texts[0]);
//Partie de prompt:
//Les bords:
draw_sprite_ext(sprite_prompt_upper_left,0,x,y,1,1,0,c_white,image_alpha);
draw_sprite_ext(sprite_prompt_upper_right,0,x+image_xscale,y,1,1,0,c_white,image_alpha);
draw_sprite_ext(sprite_prompt_bottom_left,0,x,y+image_yscale,1,1,0,c_white,image_alpha);
draw_sprite_ext(sprite_prompt_bottom_right,0,x+image_xscale,y+image_yscale,1,1,0,c_white,image_alpha);
//Les côtés:
draw_sprite_ext(sprite_prompt_center_left,0,x,y,1,image_yscale,0,c_white,image_alpha);
draw_sprite_ext(sprite_prompt_center_right,0,x+image_xscale,y,1,image_yscale,0,c_white,image_alpha);
draw_sprite_ext(sprite_prompt_bottom_middle,0,x,y+image_yscale,image_xscale,1,0,c_white,image_alpha);
draw_sprite_ext(sprite_prompt_upper_middle,0,x,y,image_xscale,1,0,c_white,image_alpha);

//Dessiner le texte des deux boutons.
//Texte de gauche:
draw_set_font(global.font_normal_condensed);
draw_text(round(x+(image_xscale*0.28)),round(y+string_height(texts[0])+10),texts[1]);
draw_text(round(x+(image_xscale*0.78)),round(y+string_height(texts[0])+10),texts[2]);
if Choice=1{
    //On affiche le truc à gauche.
    draw_sprite(sprite_prompt_left_button,0,round(x+(image_xscale*0.28)-(string_width(texts[1])/2)),round(y+string_height(texts[0])+10));
    draw_sprite(sprite_prompt_left_button,0,round(x+(image_xscale*0.28)+(string_width(texts[1])/2)),round(y+string_height(texts[0])+10));
    draw_sprite_ext(sprite_button_middle,0,round(x+(image_xscale*0.28)-(string_width(texts[1])/2))+1,round(y+string_height(texts[0])+10),string_width(texts[1]),1,0,c_white,image_alpha);
    }
else{
    draw_sprite(sprite_prompt_left_button,0,round(x+(image_xscale*0.78)-(string_width(texts[2])/2)),round(y+string_height(texts[0])+10));
    draw_sprite(sprite_prompt_left_button,0,round(x+(image_xscale*0.78)+(string_width(texts[2])/2)),round(y+string_height(texts[0])+10));
    draw_sprite_ext(sprite_button_middle,0,round(x+(image_xscale*0.78)-(string_width(texts[2])/2))+1,round(y+string_height(texts[0])+10),string_width(texts[2]),1,0,c_white,image_alpha);
    }






















